set(WORMHOLE_WILLIAM_LIB ${CMAKE_CURRENT_BINARY_DIR}/libwormhole_william.so)
set(WORMHOLE_WILLIAM_HEADER ${CMAKE_CURRENT_BINARY_DIR}/libwormhole_william.h)
set(WORMHOLE_WILLIAM_INCLUDE_DIR ${CMAKE_CURRENT_BINARY_DIR})
set(WORMHOLE_WILLIAM_C_DIR ${WORMHOLE_WILLIAM_GO_DIR}/c/)

if(${CMAKE_HOST_SYSTEM_NAME} STREQUAL "Windows")
  set(EXPORT set)
else()
  set(EXPORT export)
endif()

file(GLOB_RECURSE WORMHOLE_WILLIAM_SRC_FILES
  ${WORMHOLE_WILLIAM_GO_DIR}/**.go
  ${WORMHOLE_WILLIAM_GO_DIR}/c/**.c
  ${WORMHOLE_WILLIAM_GO_DIR}/c/**.h)

add_custom_command(
  OUTPUT ${WORMHOLE_WILLIAM_LIB} ${WORMHOLE_WILLIAM_HEADER}
  COMMENT Building wormhole_william shared library
  COMMAND
    ${EXPORT} CGO_ENABLED=1&&
    ${EXPORT} GOARCH=${GOARCH}&&
    ${EXPORT} GOOS=${GOOS}&&
    ${EXPORT} CC_FOR_TARGET=${CC_FOR_TARGET}&&
    ${EXPORT} CC=${CC_FOR_TARGET}&&
    go build -buildmode=c-shared -o ${WORMHOLE_WILLIAM_LIB} ${WORMHOLE_WILLIAM_C_DIR}
  USES_TERMINAL
  DEPENDS ${WORMHOLE_WILLIAM_SRC_FILES}
  WORKING_DIRECTORY ${WORMHOLE_WILLIAM_GO_DIR})

add_custom_target(
  wormhole_william_target DEPENDS ${WORMHOLE_WILLIAM_LIB} ${WORMHOLE_WILLIAM_HEADER})

add_library(
  wormhole_william
  SHARED
  IMPORTED
  GLOBAL)

add_dependencies(wormhole_william wormhole_william_target)

set_target_properties(
  wormhole_william
  PROPERTIES LINKER_LANGUAGE CXX
             IMPORTED_LOCATION ${WORMHOLE_WILLIAM_LIB}
             PUBLIC_HEADER ${WORMHOLE_WILLIAM_HEADER})
